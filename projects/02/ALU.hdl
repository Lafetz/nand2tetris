// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zp
    Mux16(a=x , b=false , sel=zx , out=x1 );
    Mux16(a=y , b=false , sel=zy , out=y1 );
    //np
    Not16(in=x1 , out=xNot );
    Mux16(a=x1 , b=xNot , sel=nx , out=x2);
    Not16(in=y1 , out=yNot );
    Mux16(a=y1 , b=yNot , sel=ny , out=y2);
    //f
    Add16(a =x2 , b =y2 , out =oo );
    And16(a=x2 , b=y2 , out=ao);
    Mux16(a=ao, b=oo , sel=f , out=o1 );
    //n
    Not16(in=o1 , out=o12 );
    Mux16(a=o1 , b=o12 , sel=no , out[15]=ngv,out[0..7]=ora,out[8..15]=orb, out=out);
    //zr
    Or8Way(in=ora , out=or2a );
    Or8Way(in=orb , out=or2b );
    Or(a=or2a , b=or2b , out=zrr );
    Mux(a=true , b=false , sel=zrr , out=zr );
    //ng
    Or(a=ngv , b=false , out=ng );
  

    
  
}